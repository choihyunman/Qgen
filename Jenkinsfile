import groovy.json.JsonOutput

def notifyMattermost(success = true) {
    def safeCommitMessage = params.COMMIT_MESSAGE.replaceAll(/\r?\n/, ' ').trim()
    def commitAuthor = params.COMMIT_AUTHOR
    def branchName = params.BRANCH_NAME?.replaceAll('refs/heads/', '') ?: 'unknown'
    def statusEmoji = success ? "âœ…" : "âŒ"
    def statusText = success ? "### ${statusEmoji} ë°°í¬ ì„±ê³µ" : "### ${statusEmoji} ë°°í¬ ì‹¤íŒ¨"

    def contentBlock = """
```
ğŸ‘¤ ${commitAuthor}
ğŸŒ¿ ${branchName}
ğŸ“ ${safeCommitMessage}
```
""".stripIndent().trim()

    def finalMessage = "${statusText}\n\n${contentBlock}"

    def payload = JsonOutput.toJson([text: finalMessage])

    withCredentials([string(credentialsId: 'webhook-url', variable: 'WEBHOOK_URL')]) {
        writeFile file: 'mattermost_payload.json', text: payload
        sh 'curl -X POST -H "Content-Type: application/json" -d @mattermost_payload.json "$WEBHOOK_URL"'
    }
}

def rollbackToOld() {
    echo " ë¡¤ë°± ì‹œì‘ (Old Color: ${params.OLD_COLOR})"
    sh """
        export FRONTEND_UPSTREAM=frontend_${params.OLD_COLOR}
        export BACKEND_UPSTREAM=backend_${params.OLD_COLOR}
        export AI_UPSTREAM=ai_${params.OLD_COLOR}
        envsubst '\$FRONTEND_UPSTREAM \$BACKEND_UPSTREAM \$AI_UPSTREAM' < ./nginx-template/nginx.template.conf > ./nginx/conf.d/active.conf
        docker cp ./nginx/conf.d/active.conf nginx:/etc/nginx/conf.d/active.conf
        docker exec nginx nginx -t
        docker exec nginx nginx -s reload
    """
}

pipeline {
    agent any

    parameters {
        string(name: 'COMMIT_AUTHOR', defaultValue: '', description: 'ì»¤ë°‹ ì‘ì„±ì')
        string(name: 'COMMIT_MESSAGE', defaultValue: '', description: 'ì»¤ë°‹ ë©”ì‹œì§€')
        string(name: 'BRANCH_NAME', defaultValue: '', description: 'ë¸Œëœì¹˜ ì´ë¦„')
        string(name: 'DEPLOY_COLOR', defaultValue: 'green', description: 'ë°°í¬í•  ìƒ‰ìƒ')
        string(name: 'OLD_COLOR', defaultValue: 'blue', description: 'í˜„ì¬ ìš´ì˜ ì¤‘ì¸ ìƒ‰ìƒ')
    }

    environment {
        TZ = 'Asia/Seoul'
    }

    stages {
        stage('Inject Secrets') {
            steps {
                echo "ì„¤ì • íŒŒì¼ ì£¼ì… ì¤‘..."
                withCredentials([
                    file(credentialsId: 'env-file', variable: 'ENV_FILE'),
                    file(credentialsId: 'app-yml', variable: 'APP_YML')
                ]) {
                    sh """
                        mkdir -p backend/src/main/resources
                        cp \$APP_YML backend/src/main/resources/application.yml

                        cp \$ENV_FILE backend/.env
                        cp \$ENV_FILE frontend/.env
                        cp \$ENV_FILE ai/.env

                        cp \$ENV_FILE .env
                    """
                }
            }
        }

        stage('Clean Up EXISTING NEW Containers') {
            steps {
                echo "ğŸ§¹ ê¸°ì¡´ ${params.DEPLOY_COLOR} ì»¨í…Œì´ë„ˆ ì •ë¦¬ ì¤‘..."
                sh """
                    docker compose --project-name=${params.DEPLOY_COLOR} -f docker-compose.${params.DEPLOY_COLOR}.yml down || true
                """
            }
        }

        stage('Deploy NEW Containers') {
            steps {
                echo "ìƒˆë¡œìš´ ${params.DEPLOY_COLOR} ì»¨í…Œì´ë„ˆ ë„ìš°ëŠ” ì¤‘..."
                sh """
                    docker compose --project-name=${params.DEPLOY_COLOR} -f docker-compose.${params.DEPLOY_COLOR}.yml up -d --build
                """
            }
        }

        stage('Health Check NEW Containers') {
            steps {
                sleep(time: 5, unit: 'SECONDS')
                echo " ìƒˆë¡œ ë„ìš´ ì»¨í…Œì´ë„ˆ í—¬ìŠ¤ì²´í¬ ì¤‘..."
                script {
                    def services = ["frontend_${params.DEPLOY_COLOR}", "backend_${params.DEPLOY_COLOR}", "ai_${params.DEPLOY_COLOR}"]
                    for (svc in services) {
                        retry(10) {
                            sh """
                                echo "ğŸ” Checking health of ${svc}..."
                                STATUS=\$(docker inspect --format='{{.State.Health.Status}}' ${svc} | tr -d '\\n')
                                echo "Current STATUS: \$STATUS"
                                if [ "\$STATUS" != "healthy" ]; then
                                    echo "Still not healthy (\$STATUS). Waiting 5s..."
                                    sleep 5
                                    exit 1
                                fi
                                echo "${svc} is healthy!"
                            """
                        }
                    }
                }
            }
        }

        stage('Update Nginx Configuration') {
            steps {
                echo " NGINX ì„¤ì • íŒŒì¼ ìƒì„± ì¤‘..."
                sh """
                    export FRONTEND_UPSTREAM=frontend_${params.DEPLOY_COLOR}
                    export BACKEND_UPSTREAM=backend_${params.DEPLOY_COLOR}
                    export AI_UPSTREAM=ai_${params.DEPLOY_COLOR}
                    envsubst '\$FRONTEND_UPSTREAM \$BACKEND_UPSTREAM \$AI_UPSTREAM' < ./nginx-template/nginx.template.conf > ./nginx/conf.d/active.conf
                    docker cp ./nginx/conf.d/active.conf nginx:/etc/nginx/conf.d/active.conf
                """
            }
        }

        stage('Reload Nginx') {
            steps {
                echo "NGINX ì„¤ì • ë°˜ì˜ (reload) ì¤‘..."
                script {
                    try {
                        sh """
                            docker exec nginx nginx -t
                            docker exec nginx nginx -s reload
                        """
                    } catch (Exception e) {
                        echo " Nginx reload ì‹¤íŒ¨. ë¡¤ë°± ì‹œì‘..."
                        rollbackToOld()
                        error(" ë¡¤ë°± í›„ ì‹¤íŒ¨ ì²˜ë¦¬")
                    }
                }
            }
        }

        stage('Clean Up OLD Containers') {
            steps {
                echo "ğŸ§¹ ì´ì „ (${params.OLD_COLOR}) ì»¨í…Œì´ë„ˆ ì •ë¦¬ ì¤‘..."
                sh """
                    docker compose --project-name=${params.OLD_COLOR} -f docker-compose.${params.OLD_COLOR}.yml down || true
                    docker image prune -f || true
                """
            }
        }
    }

    post {
        success {
            script {
                notifyMattermost(true)
            }
        }
        failure {
            script {
                notifyMattermost(false)
            }
        }
    }
}
